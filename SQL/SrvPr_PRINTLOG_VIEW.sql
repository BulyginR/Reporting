USE [GHOST]
exec [_DeleteProcIfExists] 'SrvPr_PRINTLOG_VIEW'
GO
CREATE PROCEDURE [WH1].[SrvPr_PRINTLOG_VIEW]

@DOCSREPORTS VARCHAR(MAX) = null,
@DOCS VARCHAR(MAX) = null,
@REPORTS VARCHAR(MAX) = null,
@ORIGINAL int = -1,
@PRINTFORMAT int = -1,
@FORCEDCOPY int = -1,
@FORCEDPRINT int = -1,
@DateFrom datetime = null,
@DateTo datetime = null,
@UserName [varchar](100) = null,
@WORKSTATION [varchar](200) = null,
@STORERKEY VARCHAR(MAX) = null

AS
BEGIN
	 SET NOCOUNT ON
	 DECLARE @WAREHOUSEPROC nvarchar(10)
	 SET @WAREHOUSEPROC='WH1'

	 if OBJECT_ID('tempdb..#TBL_DOCSREPORTS') is not null
		 drop table #TBL_DOCSREPORTS
	 if OBJECT_ID('tempdb..#TBL_DOCS') is not null
		 drop table #TBL_DOCS
	 if OBJECT_ID('tempdb..#TBL_REPORTS') is not null
		 drop table #TBL_REPORTS
	 if OBJECT_ID('tempdb..#TBL_STORERKEY') is not null
		 drop table #TBL_STORERKEY

	 CREATE TABLE #TBL_DOCSREPORTS (IDENT INT, DOCNUMBER  [varchar](100), REPORTNAME [varchar](100))
	 CREATE TABLE #TBL_DOCS (DOCNUMBER [varchar](100))
	 CREATE TABLE #TBL_REPORTS (REPORTNAME [varchar](100))
	 CREATE TABLE #TBL_STORERKEY (STORERKEY [varchar](15))

	 IF ISNULL(@DOCSREPORTS,'') <> ''
		  INSERT INTO #TBL_DOCSREPORTS (IDENT, DOCNUMBER, REPORTNAME)
		  SELECT 
				ROW_NUMBER() OVER (ORDER BY (SELECT 1)) AS IDENT,
				CAST(SUBSTRING(ITEM,1,CASE WHEN CHARINDEX('#',ITEM)>0 THEN CHARINDEX('#',ITEM)-1 ELSE 0 END) AS VARCHAR(500)) AS DOCNUMBER,
				CAST(SUBSTRING(ITEM,CHARINDEX('#',ITEM)+1, LEN(ITEM)) AS VARCHAR(500)) AS REPORTNAME
		  FROM PRD1.DBO.SPLITLIST(@DOCSREPORTS,'|')

	 IF ISNULL(@DOCS,'') <> ''
		  INSERT INTO #TBL_DOCS (DOCNUMBER)
		  SELECT ITEM AS DOCNUMBER FROM PRD1.DBO.SPLITLIST(@DOCS,'|')

	 IF ISNULL(@REPORTS,'') <> ''
		  INSERT INTO #TBL_REPORTS (REPORTNAME)
		  SELECT ITEM AS REPORTNAME FROM PRD1.DBO.SPLITLIST(@REPORTS,'|')

	 IF ISNULL(@STORERKEY,'') <> ''
		  INSERT INTO #TBL_STORERKEY (STORERKEY)
		  SELECT ITEM AS DOCNUMBER FROM PRD1.DBO.SPLITLIST(@STORERKEY,'|')

	 --delete from #TBL_REPORTS where REPORTNAME in (select REPORTNAME from #TBL_DOCSREPORTS)
	 --set @REPORTS = ''
	 --select @REPORTS = @REPORTS + '|' + REPORTNAME  from #TBL_REPORTS
	 --set @REPORTS =  stuff(@REPORTS,1,1,'')
	 /*select @REPORTS
	 select * from #TBL_REPORTS*/



	SELECT 
	 PL.[SERIALKEY],ISNULL(REPORTS.REPORTCAPTION,PL.[REPORTNAME]) AS [REPORTNAME],PL.[DOCNUMBER],PL.[ORIGINAL],PL.[PRINTAMOUNT],PL.[PRINTFORMAT], PL.[FORCEDCOPY],PL.[FORCEDPRINT],PL.[ADDWHO],PL.[ADDDATE],PL.[EDITWHO],PL.[EDITDATE],PL.[WORKSTATION],PL.[PAGECOUNT],PL.[STORERKEY]
	FROM SrvPr_PRINTLOG PL WITH(NOLOCK)
		 LEFT JOIN SrvPr_REPORTS REPORTS WITH(NOLOCK) ON REPORTS.REPORTNAME = PL.REPORTNAME
		 LEFT JOIN #TBL_DOCSREPORTS ON #TBL_DOCSREPORTS.DOCNUMBER=PL.DOCNUMBER AND #TBL_DOCSREPORTS.REPORTNAME=PL.REPORTNAME
		 LEFT JOIN #TBL_DOCS ON #TBL_DOCS.DOCNUMBER=PL.DOCNUMBER
		 LEFT JOIN #TBL_REPORTS ON #TBL_REPORTS.REPORTNAME=PL.REPORTNAME
		 LEFT JOIN #TBL_STORERKEY ON #TBL_STORERKEY.STORERKEY=PL.STORERKEY
	WHERE 
		  (ISNULL(@DOCSREPORTS,'') = '' OR #TBL_DOCSREPORTS.DOCNUMBER IS NOT NULL)
		  AND (ISNULL(@DOCS,'') = '' OR #TBL_DOCS.DOCNUMBER IS NOT NULL)
		  AND (ISNULL(@REPORTS,'') = '' OR #TBL_REPORTS.REPORTNAME IS NOT NULL)
		  AND (@DateFrom IS NULL OR PL.[ADDDATE] >= @DateFrom)
		  AND (@DateTo IS NULL OR 
						  ( 
								(@DateTo = DATEADD(DAY, DATEDIFF(DAY, 0, @DateTo), 0) and PL.[ADDDATE] < DATEADD(DAY, 1, @DateTo )) OR 
								(@DateTo <> DATEADD(DAY, DATEDIFF(DAY, 0, @DateTo), 0) and PL.[ADDDATE] <= @DateTo) 
						  ) 
				)
		  AND (@ORIGINAL = -1 OR PL.[ORIGINAL] = @ORIGINAL)
		  AND (@PRINTFORMAT = -1 OR PL.[PRINTFORMAT] = @PRINTFORMAT)
		  AND (@FORCEDCOPY = -1 OR PL.[FORCEDCOPY] = @FORCEDCOPY)
		  AND (@FORCEDPRINT = -1 OR PL.[FORCEDPRINT] = @FORCEDPRINT)
		  AND (ISNULL(@UserName,'') = '' OR PL.[ADDWHO] LIKE '%' + @UserName + '%')
		  AND (ISNULL(@WORKSTATION,'') = '' OR PL.[WORKSTATION] LIKE '%' + @WORKSTATION + '%')
		  AND (ISNULL(@STORERKEY,'') = '' OR #TBL_STORERKEY.[STORERKEY] IS NOT NULL)
	 ORDER BY PL.ADDDATE DESC
END
GO
GRANT EXEC ON  [WH1].[SrvPr_PRINTLOG_VIEW] TO [PUBLIC]

GO
exec [_CopyProcForAllSchemas] 'SrvPr_PRINTLOG_VIEW'
